$ENTRY IdentifierList {
    /* IdentifierList -> IDENTIFIER ',' IdentifierList */
    () (IDENTIFIER e.I) e.Tokens t.Errs
        = <IdentifierList ((IDENTIFIER e.I)) e.Tokens t.Errs>;
    () (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <IdentifierList ()
            e.Tokens (e.Errs (t.Start IdentifierList '.'
                Unexpected t.Type '.'
                Expected '(' IDENTIFIER ').'
                Skipped '.'))>;
    () '$' (e.Errs)
        = (IdentifierList (EOF)) '$'
            (e.Errs (IdentifierList '.'
                Unexpected End Of File '.'
                Expected '(' IDENTIFIER ').'));
    
    (t.I) (',' e.C) e.Tokens t.Errs
        = <IdentifierList (t.I (',' e.C)) e.Tokens t.Errs>;

    (t.I t.C) (IdentifierList e.IL) e.Tokens t.Errs
        = <IdentifierList (t.I t.C (IdentifierList e.IL)) e.Tokens t.Errs>;
    (t.I t.C) e.Tokens t.Errs
        = <IdentifierList (t.I t.C) <IdentifierList () e.Tokens t.Errs>>;
    
    (t.I t.C t.IL) e.Tokens t.Errs
        = (IdentifierList (IDENTIFIER ',' IdentifierList) t.I t.C t.IL) e.Tokens t.Errs;
    
    /* IdentifierList -> IDENTIFIER */
    (t.I) e.Tokens t.Errs
        = (IdentifierList (IDENTIFIER) t.I) e.Tokens t.Errs;
}
