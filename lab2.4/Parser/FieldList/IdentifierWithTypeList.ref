*$FROM Parser/FieldList/IdentifierWithType
$EXTERN IdentifierWithType;

$ENTRY IdentifierWithTypeList {
    /* IdentifierWithTypeList -> IdentifierWithType ';' IdentifierWithTypeList */
    () (IdentifierWithType e.I) e.Tokens t.Errs
        = <IdentifierWithTypeList ((IdentifierWithType e.I)) e.Tokens t.Errs>;
    () e.Tokens t.Errs
        = <IdentifierWithTypeList () <IdentifierWithType () e.Tokens t.Errs>>;
    
    (t.I) (';' e.C) e.Tokens t.Errs
        = <IdentifierWithTypeList (t.I (';' e.C)) e.Tokens t.Errs>;
    (t.I) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <IdentifierWithTypeList (t.I)
            (';' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs) 
            e.Tokens (e.Errs (t.Start IdentifierWithTypeList '.'
                Unexpected t.Type '.'
                Expected '(' ';' ').'
                Inserted '.'))>;
    
    (t.I t.C) (IdentifierWithTypeList e.IL) e.Tokens t.Errs
        = <IdentifierWithTypeList (t.I t.C (IdentifierWithTypeList e.IL)) e.Tokens t.Errs>;
    (t.I t.C) (IDENTIFIER e.ID) e.Tokens t.Errs
        = <IdentifierWithTypeList (t.I t.C)
            <IdentifierWithTypeList () (IDENTIFIER e.ID) e.Tokens t.Errs>>;
    (t.I t.C) e.Tokens t.Errs
        = (IdentifierWithTypeList (IdentifierWithType) t.I) t.C e.Tokens t.Errs;
    
    (t.I t.C t.IL) e.Tokens t.Errs
        = (IdentifierWithTypeList (IdentifierWithType ';' IdentifierWithTypeList) t.I t.C t.IL)
            e.Tokens t.Errs;
    
    /* IdentifierWithTypeList -> IdentifierWithType */
    (t.I) e.Tokens t.Errs
        = (IdentifierWithTypeList (IdentifierWithType) t.I) e.Tokens t.Errs;
}
