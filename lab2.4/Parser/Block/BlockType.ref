*$FROM Parser/Type/Type
$EXTERN Type_;

$ENTRY BlockType {
    /* BlockType -> IDENTIFIER '=' Type ';' */
    () (IDENTIFIER e.I) e.Tokens t.Errs
        = <BlockType ((IDENTIFIER e.I)) e.Tokens t.Errs>;
    () (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <BlockType ()
            e.Tokens (e.Errs (t.Start BlockType '.'
                Unexpected t.Type '.'
                Expected '(' IDENTIFIER ').'
                Skipped '.'))>;
    () '$' (e.Errs)
        = (BlockType (EOF)) '$'
            (e.Errs (BlockType '.'
                Unexpected End Of File '.'
                Expected '(' IDENTIFIER ').'));

    (t.I) ('=' e.E) e.Tokens t.Errs
        = <BlockType (t.I ('=' e.E)) e.Tokens t.Errs>;
    (t.I) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <BlockType (t.I)
            ('=' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs)
            e.Tokens (e.Errs (t.Start BlockType '.'
                Unexpected t.Type '.'
                Expected '(' '=' ').'
                Inserted '.'))>;
    (t.I) '$' (e.Errs)
        = (BlockType (EOF)) '$'
            (e.Errs (BlockType '.'
                Unexpected End Of File '.'
                Expected '(' '=' ').'));

    (t.I t.E) (Type e.C) e.Tokens t.Errs
        = <BlockType (t.I t.E (Type e.C)) e.Tokens t.Errs>;
    (t.I t.E) e.Tokens t.Errs
        = <BlockType (t.I t.E) <Type_ () e.Tokens t.Errs>>;
    
    (t.I t.E t.C) (';' e.S) e.Tokens t.Errs
        = <BlockType (t.I t.E t.C (';' e.S)) e.Tokens t.Errs>;
    (t.I t.E t.C) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <BlockType (t.I t.E t.C)
            (';' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs)
            e.Tokens (e.Errs (t.Start BlockType '.'
                Unexpected t.Type '.'
                Expected '(' ';' ').'
                Inserted '.'))>;
    (t.I t.E t.C) '$' (e.Errs)
        = (BlockType (EOF)) '$'
            (e.Errs (BlockType '.'
                Unexpected End Of File '.'
                Expected '(' ';' ').'));
    
    (t.I t.E t.C t.S) e.Tokens t.Errs
        = (BlockType (IDENTIFIER '=' Type ';') t.I t.E t.C t.S) e.Tokens t.Errs;
}
