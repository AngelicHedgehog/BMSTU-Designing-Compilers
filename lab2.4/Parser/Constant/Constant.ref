*$FROM Parser/Constant/UnarSign
*$FROM Parser/Constant/ConstantIdentifier
$EXTERN UnarSign, ConstantIdentifier;

$ENTRY Constant {
    /* Constant -> UnarSign ConstantIdentifier */
    () (UnarSign e.U) e.Tokens t.Errs
        = <Constant ((UnarSign e.U)) e.Tokens t.Errs>;
    () ('+' e.P) e.Tokens t.Errs
        = <Constant () <UnarSign () ('+' e.P) e.Tokens t.Errs>>;
    () ('-' e.M) e.Tokens t.Errs
        = <Constant () <UnarSign () ('-' e.M) e.Tokens t.Errs>>;
    
    ((UnarSign e.U)) (ConstantIdentifier e.C) e.Tokens t.Errs
        = <Constant ((UnarSign e.U) (ConstantIdentifier e.C)) e.Tokens t.Errs>;
    ((UnarSign e.U)) (IDENTIFIER e.I) e.Tokens t.Errs
        = <Constant ((UnarSign e.U)) <ConstantIdentifier () (IDENTIFIER e.I) e.Tokens t.Errs>>;
    
    ((UnarSign e.U) (ConstantIdentifier e.C)) e.Tokens t.Errs
        = (Constant (UnarSign ConstantIdentifier) (UnarSign e.U) (ConstantIdentifier e.C)) e.Tokens t.Errs;
    
    /* Constant -> ConstantIdentifier */
    () (ConstantIdentifier e.C) e.Tokens t.Errs
        = <Constant ((ConstantIdentifier e.C)) e.Tokens t.Errs>;
    () (IDENTIFIER e.I) e.Tokens t.Errs
        = <Constant () <ConstantIdentifier () (IDENTIFIER e.I) e.Tokens t.Errs>>;

    ((ConstantIdentifier e.C)) e.Tokens t.Errs
        = (Constant (ConstantIdentifier) (ConstantIdentifier e.C)) e.Tokens t.Errs;

    /* Constant -> UnarSign UNSIGNED_NUMBER */
    ((UnarSign e.U)) (UNSIGNED_NUMBER e.UN) e.Tokens t.Errs
        = <Constant ((UnarSign e.U) (UNSIGNED_NUMBER e.UN)) e.Tokens t.Errs>;
    ((UnarSign e.U)) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <Constant ((UnarSign e.U))
            e.Tokens (e.Errs (t.Start Constant '.'
                Unexpected t.Type '.'
                Expected '(' ConstantIdentifier or UNSIGNED_NUMBER ').'
                Skipped '.'))>;

    ((UnarSign e.U) (UNSIGNED_NUMBER e.UN)) e.Tokens t.Errs
        = (Constant (UnarSign UNSIGNED_NUMBER) (UnarSign e.U) (UNSIGNED_NUMBER e.UN)) e.Tokens t.Errs;
    
    /* Constant -> UNSIGNED_NUMBER */
    () (UNSIGNED_NUMBER e.UN) e.Tokens t.Errs
        = <Constant ((UNSIGNED_NUMBER e.UN)) e.Tokens t.Errs>;

    ((UNSIGNED_NUMBER e.UN)) e.Tokens t.Errs
        = (Constant (UNSIGNED_NUMBER) (UNSIGNED_NUMBER e.UN)) e.Tokens t.Errs;

    /* Constant -> '\'' CHAR_SEQUENCE '\'' */
    () ('\'' e.C1) e.Tokens t.Errs
        = <Constant (('\'' e.C1)) e.Tokens t.Errs>;
    
    (('\'' e.C1)) (CHAR_SEQUENCE e.CS) e.Tokens t.Errs
        = <Constant (('\'' e.C1) (CHAR_SEQUENCE e.CS)) e.Tokens t.Errs>;
    (('\'' e.C1)) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <Constant (('\'' e.C1))
            e.Tokens (e.Errs (t.Start Constant '.'
                Unexpected t.Type '.'
                Expected '(' CHAR_SEQUENCE ').'
                Skipped '.'))>;
    
    (('\'' e.C1) t.CS) ('\'' e.C2) e.Tokens t.Errs
        = <Constant (('\'' e.C1) t.CS ('\'' e.C2)) e.Tokens t.Errs>;
    (('\'' e.C1) t.CS) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <Constant (('\'' e.C1) t.CS)
            ('\'' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs)
            e.Tokens (e.Errs (t.Start Constant '.'
                Unexpected t.Type '.'
                Expected '(' '\'' ').'
                Inserted '.'))>;
    
    (('\'' e.C1) t.CS t.C2) e.Tokens t.Errs
        = (Constant ('\'' CHAR_SEQUENCE '\'') (('\'' e.C1) t.CS t.C2) e.Tokens t.Errs) e.Tokens t.Errs;
    
        
    () (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <Constant (t.C t.I)
            e.Tokens (e.Errs (t.Start Constant '.'
                Unexpected t.Type '.'
                Expected '(' UnarSign or ConstantIdentifier or UNSIGNED_NUMBER or '\'' ').'
                Skipped '.'))>;
}
