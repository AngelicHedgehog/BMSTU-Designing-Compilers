*$FROM Parser/SimpleType/IdentifierList
*$FROM Parser/Type/Type
$EXTERN IdentifierList, Type_;

$ENTRY IdentifierWithType {
    /* IdentifierWithType -> IdentifierList ':' Type */
    () (IdentifierList e.I) e.Tokens t.Errs
        = <IdentifierWithType ((IdentifierList e.I)) e.Tokens t.Errs>;
    () e.Tokens t.Errs
        = <IdentifierWithType () <IdentifierList () e.Tokens t.Errs>>;
    
    (t.I) (':' e.D) e.Tokens t.Errs
        = <IdentifierWithType (t.I (':' e.D)) e.Tokens t.Errs>;
    (t.I) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <IdentifierWithType (t.I)
            (':' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs) 
            e.Tokens (e.Errs (t.Start IdentifierWithType '.'
                Unexpected t.Type '.'
                Expected '(' ':' ').'
                Inserted '.'))>;
    
    (t.I t.D) (Type e.T) e.Tokens t.Errs
        = <IdentifierWithType (t.I t.D (Type e.T)) e.Tokens t.Errs>;
    (t.I t.D) e.Tokens t.Errs
        = <IdentifierWithType (t.I t.D) <Type_ () e.Tokens t.Errs>>;
    
    (t.I t.D t.T) e.Tokens t.Errs
        = (IdentifierWithType (IdentifierList ':' Type) t.I t.D t.T) e.Tokens t.Errs;
}
