*$FROM Parser/FieldList/IdentifierWithTypeList
*$FROM Parser/FieldList/CaseBlock
$EXTERN IdentifierWithTypeList, CaseBlock;

$ENTRY FieldList {
    /* FieldList -> IdentifierWithTypeList ';' CaseBlock */
    () (IdentifierWithTypeList e.I) e.Tokens t.Errs
        = <FieldList ((IdentifierWithTypeList e.I)) e.Tokens t.Errs>;
    () e.Tokens t.Errs
        = <FieldList () <IdentifierWithTypeList () e.Tokens t.Errs>>;
    
    (t.I) (';' e.C) e.Tokens t.Errs
        = <FieldList (t.I (';' e.C)) e.Tokens t.Errs>;
    (t.I) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <FieldList (t.I)
            (';' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs) 
            e.Tokens (e.Errs (t.Start FieldList '.'
                Unexpected t.Type '.'
                Expected '(' ';' ').'
                Inserted '.'))>;

    (t.I t.C) (CaseBlock e.CB) e.Tokens t.Errs
        = <FieldList (t.I t.C (CaseBlock e.CB)) e.Tokens t.Errs>;
    (t.I t.C) (KW_CASE e.CB) e.Tokens t.Errs
        = <FieldList (t.I t.C) <CaseBlock () (KW_CASE e.CB) e.Tokens t.Errs>>;
    
    (t.I t.C t.CB) e.Tokens t.Errs
        = (FieldList (IdentifierWithTypeList ';' CaseBlock) t.I t.C t.CB)  e.Tokens t.Errs;

    /* FieldList -> IdentifierWithTypeList */
    (t.I) e.Tokens t.Errs
        = (FieldList (IdentifierWithTypeList) t.I) e.Tokens t.Errs;
}
