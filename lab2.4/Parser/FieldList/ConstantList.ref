*$FROM Parser/Constant/Constant
$EXTERN Constant;

$ENTRY ConstantList {
    /* ConstantList -> Constant ',' ConstantList */
    () (Constant e.C) e.Tokens t.Errs
        = <ConstantList ((Constant e.C)) e.Tokens t.Errs>;
    () e.Tokens t.Errs
        = <ConstantList () <Constant () e.Tokens t.Errs>>;
    
    (t.C) (',' e.CM) e.Tokens t.Errs
        = <ConstantList (t.C (',' e.CM)) e.Tokens t.Errs>;
    (t.C) (t.Type (t.Start t.End) e.Attrs) e.Tokens (e.Errs)
        = <ConstantList (t.C)
            (',' (t.Start t.Start)) (t.Type (t.Start t.End) e.Attrs) 
            e.Tokens (e.Errs (t.Start ConstantList '.'
                Unexpected t.Type '.'
                Expected '(' ',' ').'
                Inserted '.'))>;
    
    (t.C t.CM) (ConstantList e.CL) e.Tokens t.Errs
        = <ConstantList (t.C t.CM (ConstantList e.CL)) e.Tokens t.Errs>;
    (t.C t.CM) e.Tokens t.Errs
        = <ConstantList (t.C t.CM) <ConstantList () e.Tokens t.Errs>>;
    
    (t.C t.CM t.CL) e.Tokens t.Errs
        = (ConstantList (Constant ',' ConstantList) t.C t.CM t.CL) e.Tokens t.Errs;
    
    /* ConstantList -> Constant */
    (t.C) e.Tokens t.Errs
        = (ConstantList (Constant) t.C) e.Tokens t.Errs;
}